<!DOCTYPE html>
<html lang="en">

<head>
    <title>Cron schedule using F# - 1 May, 2017</title>
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <meta name="keywords" content="dotnet,fsharp">
    <meta name="author" content="Eugene Pyl">
    <link rel="icon" href="./../favicon.ico" />
    <link rel="stylesheet" href="./../prism.css">
    
    <script type="text/javascript" async src="https://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS_CHTML"></script>
    <script type="text/javascript" src="https://platform-api.sharethis.com/js/sharethis.js#property=5906f3ca75e4e1001109c19a&product=inline-share-buttons"></script>
    <style>
        img {
            max-width: 100%;
            height: auto;
            width: auto\9;
            /* ie8 */
        }
        
        body {
            font-family: 'Verdana', sans-serif;
            line-height: 1.3rem;
            box-sizing: border-box;
            min-width: 200px;
            max-width: 980px;
            margin: 0 auto;
            padding: 1rem;
            background-image: url(./../bg02.png);
            background-color: #fff;
        }
        
        h2 {
            font-size: 1.3rem;
        }
        
        code.has-jax {
            font: inherit;
            font-size: 100%;
            background: inherit;
            border: inherit;
        }
        
        .markdown-body pre {
            border: 1px solid black;
        }
        
        div.shareit {
            margin-top: 0.5rem;
        }
        
        article>h2 {
            margin-top: 0;
        }
    </style>
</head>

<body>
    <article class="markdown-body">
        <h2 id="cron-schedule-using-f-1-may-2017">Cron schedule using F# - 1 May, 2017</h2>
<p>Tags: dotnet, fsharp</p>
<p>I decided to start learning fsharp and hope I will be able to use F# in my future small projects.</p>
<p>Currently I have a small service worked in Docker container. It is written using C# on dotnet core.
This service is for grabbing data from external services via HTTP and putting aggregated data to InfluxDB database. Internally the service is using cron to plan and run this process as we want to grab data periodically.</p>
<h4 id="i-started-with-rewriting-cron-scheduling-">I started with rewriting cron scheduling.</h4>
<p>So there is implementation in F# below:</p>
<p>Let&#39;s start with two helper objects: <code>String.split</code> and <code>TooMuchArgumentsException</code> exception.</p>
<pre><code class="language-fsharp">type System.String with 
    static member split c (value: string) =
        value.Split c

exception TooMuchArgumentsException of int
</code></pre>
<p><code>String.split</code> is just wrapper for <code>String.Split</code> method. Exception is needed to show when cron expression contains too much parts.</p>
<p>My internal cron supports the next template: &#39;minute hour dayOfMonth month dayOfWeek&#39;. And the next type of cron expressions: </p>
<ol>
<li>&#39;*&#39; - wildcard;</li>
<li>&#39;*/5&#39; - every 5th, e.g. every five minutes;</li>
<li>&#39;10-20/5&#39; - range value, e.g. every from 10 till 20, e.g. every minute from 10 till 20. &#39;/5&#39; is optional and it works the same as previous one, so &#39;10-20/5&#39; for minutes means run at 10, 15 and 20;</li>
<li>&#39;5&#39; - one value only, e.g. only at 5th minute</li>
<li>&#39;5,10,15,45&#39; - list value, e.g. run at 5th, 10th, 15th and 45th minutes</li>
</ol>
<pre><code class="language-fsharp">open System
open System.Text.RegularExpressions

module Schedule =
    // regexp for */5
    [&lt;Literal&gt;]
    let DividePattern = @&quot;(\*/\d+)&quot;
    // regexp for range
    [&lt;Literal&gt;]
    let RangePattern = @&quot;(\d+\-\d+(/\d+)?)&quot;
    // regexp for wild char
    [&lt;Literal&gt;]
    let WildPattern = @&quot;(\*)&quot;
    // regexp for one value
    [&lt;Literal&gt;]
    let OneValuePattern = @&quot;(\d)&quot;
    // regexp for list value
    [&lt;Literal&gt;]
    let ListPattern = @&quot;((\d+,)+\d+)&quot;

    // internal record to parsed cron expression, so it contains minutes,
    // hours, days of month, months and day of week when we should run our jobs
    type ISchedueSet = 
        { 
            Minutes: int list;
            Hours: int list;
            DayOfMonth: int list;
            Months: int list;
            DayOfWeek: int list
        }

    // method to generate ISchedueSet record from cron expression
    let generate expression =
        // internal method to parse */5 
        let dividedArray (m:string) start max =
            let divisor = m |&gt;  String.split [|&#39;/&#39;|] |&gt; Array.skip 1 |&gt; Array.head |&gt; Int32.Parse
            [start .. max] |&gt; List.filter (fun x -&gt; x % divisor = 0)
        // internal method to parse range
        let rangeArray (m:string) =
            let split = m |&gt; String.split [|&#39;-&#39;; &#39;/&#39;|] |&gt; Array.map Int32.Parse
            match Array.length split with
                | 2 -&gt; [split.[0] .. split.[1]]
                | 3 -&gt; [split.[0] .. split.[1]] |&gt; List.filter (fun x -&gt; x % split.[2] = 0)
                | _ -&gt; []
        // internal method to parse wild char
        let wildArray (m:string) start max =
            [start .. max]
        // internal method to parse single value
        let oneValue (m:string) =
            [m |&gt; Int32.Parse]
        // internal method to parse list value
        let listArray (m:string) =
            m |&gt; String.split [|&#39;,&#39;|] |&gt; Array.map Int32.Parse |&gt; Array.toList

        // we need to set minimum and maximum value for every part of date time
        let getStartAndMax i =
            match i with
            // for minutes
            | 0 -&gt; (0, 59)
            // for hours
            | 1 -&gt; (0, 23)
            // for days of month
            | 2 -&gt; (1, 31)
            // for months
            | 3 -&gt; (1, 12)
            // for day of week
            | 4 -&gt; (0, 6)
            // throw an exception if don&#39;t know for what part of date time we need values
            | _ -&gt; raise (TooMuchArgumentsException i)

        // active pattern to match regexp
        let (|MatchRegex|_|) pattern input =
            let m = Regex.Match(input, pattern)
            if m.Success then Some (m.ToString()) else None

        // parsing cron expression and create a array of lists which contains all possibles values
        // for every part of daytime
        let parts =
            expression 
            |&gt; String.split [|&#39; &#39;|]
            |&gt; Array.mapi (fun i x -&gt;
                let (start, max) = getStartAndMax i 
                match x with
                    | MatchRegex DividePattern x -&gt; dividedArray x start max
                    | MatchRegex RangePattern x -&gt; rangeArray x
                    | MatchRegex WildPattern x -&gt; wildArray x start max
                    | MatchRegex ListPattern x -&gt; listArray x
                    | MatchRegex OneValuePattern x -&gt; oneValue x
                    | _ -&gt; []
            )
        // convert list of array to ISchedueSet
        { 
            Minutes = parts.[0];
            Hours = parts.[1];
            DayOfMonth = parts.[2];
            Months = parts.[3];
            DayOfWeek = parts.[4]
        }
    // method to check date time via generated ISchedueSet
    let isTime schedueSet (dateTime : DateTime) =
        List.exists ((=) dateTime.Minute) schedueSet.Minutes &amp;&amp; 
        List.exists ((=) dateTime.Hour) schedueSet.Hours &amp;&amp;
        List.exists ((=) dateTime.Day) schedueSet.DayOfMonth &amp;&amp;
        List.exists ((=) dateTime.Month) schedueSet.Months &amp;&amp;
        List.exists ((=) (int dateTime.DayOfWeek)) schedueSet.DayOfWeek
</code></pre>
<p>So Schedule module contains two methods and one type: </p>
<ul>
<li>ISchedueSet is a container for parsed cron expression;</li>
<li>generate is to generate ISchedueSet record from cron expression. This record contains all possible values of minutes, hours, months, days of month, days of week for particular cron expression;</li>
<li>isTime is to check if we need to run a job in passed date time </li>
</ul>
<p>Small sets of unit tests written using mstest:</p>
<pre><code class="language-fsharp">namespace FsharpTest

open System
open Microsoft.VisualStudio.TestTools.UnitTesting
open Cron
open Schedule

// minute hour dayOfMonth month dayOfWeek
[&lt;TestClass&gt;]
type ScheduleTests () =

    [&lt;TestMethod&gt;]
    member this.All () =
        let schedule = Schedule.generate &quot;* * * * *&quot;
        Assert.IsTrue(List.forall2 ( = ) schedule.DayOfMonth [1..31]);
        Assert.IsTrue(List.forall2 ( = ) schedule.DayOfWeek [0..6]);
        Assert.IsTrue(List.forall2 ( = ) schedule.Hours [0..23]);
        Assert.IsTrue(List.forall2 ( = ) schedule.Minutes [0..59]);
        Assert.IsTrue(List.forall2 ( = ) schedule.Months [1..12]);

    [&lt;TestMethod&gt;]
    member this.Range () =
        let schedule = Schedule.generate &quot;0-5 0-10/2 10-20/3 3-5 2-6/2&quot;
        Assert.IsTrue(List.forall2 ( = ) schedule.DayOfMonth [12; 15; 18]);
        Assert.IsTrue(List.forall2 ( = ) schedule.DayOfWeek [2; 4; 6]);
        Assert.IsTrue(List.forall2 ( = ) schedule.Hours [0; 2; 4; 6; 8; 10]);
        Assert.IsTrue(List.forall2 ( = ) schedule.Minutes [0; 1; 2; 3; 4; 5]);
        Assert.IsTrue(List.forall2 ( = ) schedule.Months [3; 4; 5]);

    [&lt;TestMethod&gt;]
    member this.OneTime () =
        let schedule = Schedule.generate &quot;0 0 1 1 0&quot;
        Assert.IsTrue(List.forall2 ( = ) schedule.DayOfMonth [1]);
        Assert.IsTrue(List.forall2 ( = ) schedule.DayOfWeek [0]);
        Assert.IsTrue(List.forall2 ( = ) schedule.Hours [0]);
        Assert.IsTrue(List.forall2 ( = ) schedule.Minutes [0]);
        Assert.IsTrue(List.forall2 ( = ) schedule.Months [1]);

    [&lt;TestMethod&gt;]
    member this.Every () =
        let schedule = Schedule.generate &quot;*/15 */3 */2 */5 *&quot;
        Assert.IsTrue(List.forall2 ( = ) schedule.DayOfMonth [for i in [1 .. 31] do
                                                                if i%2 = 0 then
                                                                    yield i]
        );
        Assert.IsTrue(List.forall2 ( = ) schedule.DayOfWeek [0 .. 6]);
        Assert.IsTrue(List.forall2 ( = ) schedule.Hours [0; 3; 6; 9; 12; 15; 18; 21]);
        Assert.IsTrue(List.forall2 ( = ) schedule.Minutes [0; 15; 30; 45]);
        Assert.IsTrue(List.forall2 ( = ) schedule.Months [5; 10]);

    [&lt;TestMethod&gt;]
    member this.OnlySome () =
        let schedule = Schedule.generate &quot;1,2,3 5,6,7 28,29 1,2 5,6&quot;
        Assert.IsTrue(List.forall2 ( = ) schedule.DayOfMonth [28;29]);
        Assert.IsTrue(List.forall2 ( = ) schedule.DayOfWeek [5;6]);
        Assert.IsTrue(List.forall2 ( = ) schedule.Hours [5;6;7]);
        Assert.IsTrue(List.forall2 ( = ) schedule.Minutes [1;2;3]);
        Assert.IsTrue(List.forall2 ( = ) schedule.Months [1;2]);

    [&lt;TestMethod&gt;]
    member this.IsTime () =
        let dateTime = DateTime(2000, 1, 1, 0, 0, 0)
        let schedule = { Minutes = [0]; Hours = [0]; DayOfWeek = [0 .. 6]; DayOfMonth = [1]; Months = [1]}
        Assert.IsTrue(Schedule.isTime schedule dateTime)
</code></pre>
<p>Quite simple implementation of cron schedule part of mentioned service.</p>
<p>Thanks.</p>

    </article>
    <div class="sharethis-inline-share-buttons"></div>
    <div id="disqus_thread"></div>
    <script>
        (function() { // DON'T EDIT BELOW THIS LINE
            var d = document,
                s = d.createElement('script');
            s.src = '//eapyl-github-io.disqus.com/embed.js';
            s.setAttribute('data-timestamp', +new Date());
            (d.head || d.body).appendChild(s);
        })();
    </script>
    <script type="text/x-mathjax-config">
        MathJax.Hub.Config({ tex2jax: { skipTags: ['script', 'noscript', 'style', 'textarea', 'pre'] } }); MathJax.Hub.Queue(function() { // Fix <code> tags after MathJax finishes running. This is a
        // hack to overcome a shortcoming of Markdown. Discussion at
        // https://github.com/mojombo/jekyll/issues/199
        var all = MathJax.Hub.getAllJax(), i;
        for(i = 0; i < all.length; i += 1) {
            all[i].SourceElement().parentNode.className += ' has-jax';
        }
    });
    </script>
<script>
    (function (i, s, o, g, r, a, m) {
        i['GoogleAnalyticsObject'] = r; i[r] = i[r] || function () {
            (i[r].q = i[r].q || []).push(arguments)
        }, i[r].l = 1 * new Date(); a = s.createElement(o),
            m = s.getElementsByTagName(o)[0]; a.async = 1; a.src = g; m.parentNode.insertBefore(a, m)
    })(window, document, 'script', 'https://www.google-analytics.com/analytics.js', 'ga');

    ga('create', 'UA-87583712-1', 'auto');
    ga('send', 'pageview');

</script>
<script src="./../prism.js"></script>
</body>

</html>